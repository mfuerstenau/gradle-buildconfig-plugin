plugins {
    id 'java'
    id "com.gradle.plugin-publish" version "0.9.1"
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

group = 'de.fuerstenau'
version = '1.0.1'

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    provided gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.assertj:assertj-core:2.1.0'
}


jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

pluginBundle {
  // These settings are set for the whole plugin bundle
  website = 'https://github.com/mfuerstenau/gradle-buildconfig-plugin'
  vcsUrl = 'https://github.com/mfuerstenau/gradle-buildconfig-plugin'

  plugins {

    // another plugin
    buildConfigPlugin {
      id = 'de.fuerstenau.buildconfig'
      displayName = 'Java/Groovy BuildConfig plugin'
      description = 'Provides per SourceSet BuildConfig static class for gradle Java/Groovy projects'
      tags = ['java', 'buildconfig', 'static', 'version', 'generated', 'sourceset', 'groovy']
      version = project.version
    }
  }
}