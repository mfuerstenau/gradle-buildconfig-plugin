plugins {
    id 'groovy'
    id 'maven'
    id "com.gradle.plugin-publish" version "0.9.4"
}

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

group = 'de.fuerstenau'
version = '1.1.2'

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    provided gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.assertj:assertj-core:2.1.0'
}


jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

pluginBundle {
  // These settings are set for the whole plugin bundle
  website = 'https://github.com/mfuerstenau/gradle-buildconfig-plugin'
  vcsUrl = 'https://github.com/mfuerstenau/gradle-buildconfig-plugin'

  plugins {

    // another plugin
    buildConfigPlugin {
      id = 'de.fuerstenau.buildconfig'
      displayName = 'Build config plugin for Gradle Java and Groovy projects'
      description = 'A build config is a generated class holding constants set by the build script. It can be accessed within the Java or Groovy application, thus providing a way to transport information about version, project name or debug flags or a lot more info that otherwise will not be available or has to to be transported via complicated workarounds.'
      tags = ['java', 'buildconfig', 'static', 'version', 'generated', 'sourceset', 'groovy', 'constants']
      version = project.version
    }
  }
}

task wrapper(type: Wrapper) {
   gradleVersion = '2.13'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}